plugins {
    id 'java-base'
}

group 'com.fox2code'
version project['foxevents.version']

tasks.register('publishToMavenLocal', Task)

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = 'com.fox2code.foxevents'
    version = project['foxevents.version']

    repositories {
        mavenCentral()
        maven {
            url 'https://cdn.fox2code.com/maven/'
        }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }

        withSourcesJar()
        withJavadocJar()
    }

    jar {
        from "../LICENSE"
    }

    test {
        useJUnitPlatform()
    }

    afterEvaluate {
        rootProject.tasks.publishToMavenLocal.dependsOn(publishToMavenLocal)

        publishing {
            repositories {
                maven {
                    url = rootProject.layout.buildDirectory.dir('repo')
                }
            }
            publications {
                release(MavenPublication) {
                    from components.java
                    groupId = "com.fox2code.foxevents"
                    artifactId = project.name
                    pom {
                        url = 'https://github.com/Fox2Code/FoxEvents'
                    }
                }
            }
        }
    }
}
